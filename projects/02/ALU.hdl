// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:


    // Zero X and Y
    Mux16(a=x, b=false, sel=zx, out=xo); // Choose between X (0) or 0 (1)
    Mux16(a=y, b=false, sel=zy, out=yo); // Choose between Y (0) or 0 (1)

    // Not X and Y
    Not16(in=xo, out=notx); // Not the X 
    Mux16(a=xo, b=notx, sel=nx, out=ndx); // Choose between X (0) or Not X (1)

    Not16(in=yo, out=noty); // Not the y
    Mux16(a=yo, b=noty, sel=ny, out=ndy); // Choose between Y (0) or Not Y (1)


    // Add X and Y or And X and Y
    And16(a=ndx, b=ndy, out=andxy); // And X and Y
    Add16(a=ndx, b=ndy, out=plusxy); // Or X and Y

    Mux16(a=andxy, b=plusxy, sel=f, out=fa); // Choose between And (0) or Or (1)

    // Not Out
    Not16(in=fa, out=notfa); // Not the fa
    Mux16(a=fa, b=notfa, sel=no, out=out, out[15]=outter, out[0..7]=tophalf, out[8..15]=bottomhalf); // Choose between fa (0) or Not fa (1


    And(a=outter, b=true, out=ng); // And the out[15] with true

    Or8Way(in=tophalf, out=top);
    Or8Way(in=bottomhalf, out=bottom);
    Or(a=top, b=bottom, out=orout); // Or the top and bottom halfs
    Not(in=orout, out=zr);
}