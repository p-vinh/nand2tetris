// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS: // order is dependent
    // Increment
    Inc16(in=oo, out=incr);
    Mux16(a=oo, b=incr, sel=inc, out=rest);
    // Load
    Mux16(a=rest, b=in, sel=load, out=ret1);
    // Reset
    Mux16(a=ret1, b[0..15]=false, sel=reset, out=ret);
    // Else
    Register(in=ret, load=true, out=out, out=oo);
}
