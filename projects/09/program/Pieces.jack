
class Pieces {
	field int type; // 1 Cross, 2 Circle
	field int width, height;
	
	constructor Pieces new(int t) {
		let type = t;
		
        let width = 16;
        let height = 16;

		return this;
	}
	
	method int getType() {
		return type;
	}
	
	method void drawPiece(int x, int y) {
		if(type = 1) {
			do drawCross(x, y);
		} else {
			do drawCircle(x, y);
		}
		return;
	}
	
	method void drawCircle(int x, int y) {
		var int radiusOutter, radiusInner, t;
		let t = 4;
		let radiusOutter = width / 2;
		let radiusInner = radiusOutter - t;
		
		do Screen.drawCircle(x + radiusOutter, y + radiusOutter, radiusOutter);
		do Screen.setColor(false);
		do Screen.drawCircle(x + radiusOutter, y + radiusOutter, radiusInner);
		do Screen.setColor(true);
		return;
	}
	
	method void drawCross(int x, int y) {
		var int t, i;
		let t = 4;
		let i = t - 1;
		while(~(i < 0)) {
		
			do Screen.drawLine(x, y + height - i,x + width - i, y);
			do Screen.drawLine(x + i, y + height, x + width, y + i);
			
			
			do Screen.drawLine(x + i, y, x + width, y + height - i);
			do Screen.drawLine(x, y + i, x + width - i, y + height);
			
			let i = i - 1;
		}
		return;
	}
	
	method int getHeight() {
		return height;
	}
	
	method int getWidth() {
		return width;
	}

}